plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.11.2"
}

repositories {
    mavenCentral()
}

def restAssured = "5.0.1",
    junitVersion = "5.8.2",
    slf4jVersion = "1.7.36"

dependencies {
    testImplementation(
            "io.rest-assured:rest-assured:$restAssured",
            "io.rest-assured:json-schema-validator:$restAssured",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            'io.qameta.allure:allure-rest-assured:2.20.1',
            'org.assertj:assertj-core:3.24.2',
            "org.slf4j:slf4j-simple:$slf4jVersion")
}

allure {
    report {
        version.set("2.17.2")
    }
    adapter {
        setAspectjWeaver(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.17.2")
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Test).configureEach {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
//            events "started", "failed"
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

